plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
}

group = 'net.prosavage'
version = '0.0.1a'
description = 'Yet another basic, free and customizable RPG plugin that was made because there weren\'t any basic free ones out there.'
sourceCompatibility = '17'
jar.enabled = false

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = uri('https://hub.spigotmc.org/nexus/content/groups/public/') }
    maven { url = uri('https://oss.sonatype.org/content/groups/public/') }
    maven { url = uri('https://oss.sonatype.org/content/repositories/snapshots') }
    maven { url = uri('https://repo.maven.apache.org/maven2') }
    maven { url = uri('https://repo.aikar.co/content/groups/aikar/') }
    maven { url = uri('https://repo.jeff-media.com/public/')}
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'com.ezylang:EvalEx:3.0.5'
    implementation 'net.kyori:adventure-text-minimessage:4.14.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.0'
    implementation 'com.tchristofferson:ConfigUpdater:2.1-SNAPSHOT'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation 'com.jeff_media:ArmorEquipEvent:1.0.2-SNAPSHOT'
}

tasks.build.dependsOn tasks.shadowJar

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    filter{
        String line -> line.replaceAll('\\$\\{project.version}', version as String)
    }
}

shadowJar {
    mergeServiceFiles()
    relocate 'com.udojava', 'net.prosavage.yarpg.libs.com.udojava'
    relocate 'co.aikar.commands', 'net.prosavage.yarpg.libs.acf'
    relocate 'co.aikar.locales', 'net.prosavage.yarpg.libs.acf_locales'
}
